      *===============================================================* 00001068
       IDENTIFICATION                            DIVISION.              00002068
      *===============================================================* 00002168
                                                                        00004068
       PROGRAM-ID.  F10CIP2.                                            00005068
       AUTHOR.      ADNAN.                                              00006068
                                                                        00006168
      *===============================================================* 00006368
      *                     F O U R S Y S                             * 00006468
      *===============================================================* 00006568
      *    AUTOR    : ADNAN CHRISTIAN                           *       00006768
      *---------------------------------------------------------*       00006868
      *    EMPRESA  : FOURSYS                                   *       00006968
      *---------------------------------------------------------*       00007068
      *    DATA     : 01/07/2022                                *       00007168
      *---------------------------------------------------------*       00007268
      *    OBJETIVO : APLICAR CONHECIMENTO DO COMANDO           *       00008068
      *               CICS, COM DB2                             *       00009068
      *=========================================================*       00010068
                                                                        00020068
                                                                        00030068
      *=========================================================*       00040068
       ENVIRONMENT                               DIVISION.              00050068
      *=========================================================*       00060068
                                                                        00070068
      *---------------------------------------------------------*       00070168
       CONFIGURATION                             SECTION.               00070268
      *---------------------------------------------------------*       00070368
                                                                        00070468
       SPECIAL-NAMES.                                                   00070568
            DECIMAL-POINT IS COMMA.                                     00070668
                                                                        00070768
      *=========================================================*       00070868
       DATA                                      DIVISION.              00070968
      *=========================================================*       00071068
                                                                        00071168
      *---------------------------------------------------------*       00071268
       WORKING-STORAGE                            SECTION.              00071368
      *---------------------------------------------------------*       00071468
                                                                        00071568
      ***********************************************************       00071668
      *                       AREA CICS                         *       00071768
      ***********************************************************       00071868
                                                                        00071968
           COPY F10CIM1.                                                00072069
                                                                        00072168
      ***********************************************************       00072268
      *                   AREA DA WRK-DADOS                     *       00072368
      ***********************************************************       00072468
                                                                        00072568
           COPY 'B#FUNC'.                                               00072668
                                                                        00072768
      ***********************************************************       00072868
      *                   AREA DE AUXILIARES                    *       00072968
      ***********************************************************       00073068
                                                                        00074068
       77 WRK-EMAIL-NULL                        PIC S9(04) COMP.        00075068
       77 WRK-SQLCODE                           PIC -999.               00075168
       77 WRK-FLAG                              PIC 9(01).              00075268
       77 WRK-RET-CICS                          PIC S9(04) COMP.        00075368
       77 WRK-SAIDA                             PIC X(30).              00075468
                                                                        00075568
      *=========================================================*       00075668
       PROCEDURE                                 DIVISION.              00075768
      *=========================================================*       00075868
                                                                        00075968
      ***********************************************************       00076068
      *               PROCESSAMENTO PRINCIPAL                   *       00077068
      ***********************************************************       00078068
                                                                        00079068
      *---------------------------------------------------------*       00079168
       0000-PRINCIPAL                            SECTION.               00079268
      *---------------------------------------------------------*       00079368
                                                                        00079468
           PERFORM 1000-INICIAR.                                        00079568
                                                                        00079668
           IF WRK-FLAG EQUAL 2                                          00079768
            PERFORM 2000-PROCESSAR                                      00079868
           END-IF.                                                      00079968
                                                                        00080068
           PERFORM 3000-FINALIZAR.                                      00080168
                                                                        00080268
           EXEC CICS                                                    00080368
             RETURN TRANSID('T102')                                     00080469
           END-EXEC.                                                    00080568
                                                                        00080668
      *---------------------------------------------------------*       00080768
       0000-99-FIM.                  EXIT.                              00080868
      *---------------------------------------------------------*       00080968
                                                                        00081068
      *---------------------------------------------------------*       00082068
       1000-INICIAR                              SECTION.               00082168
      *---------------------------------------------------------*       00082268
                                                                        00082368
           EXEC CICS RECEIVE                                            00082468
             MAPSET('F10CIM1')                                          00082569
             MAP('MAPTEST')                                             00082668
             INTO(MAPTESTI)                                             00082768
             RESP(WRK-RET-CICS)                                         00082868
           END-EXEC.                                                    00082968
                                                                        00083068
           IF WRK-RET-CICS = DFHRESP(MAPFAIL)                           00083168
              MOVE 1 TO WRK-FLAG                                        00083268
           ELSE                                                         00083368
              MOVE 2 TO WRK-FLAG                                        00083468
           END-IF.                                                      00083568
                                                                        00083668
      *---------------------------------------------------------*       00083768
       1000-99-FIM.                     EXIT.                           00083868
      *---------------------------------------------------------*       00083968
                                                                        00084068
      *---------------------------------------------------------*       00084168
       2000-PROCESSAR                            SECTION.               00084268
      *---------------------------------------------------------*       00084368
                                                                        00084468
      *************************************************************     00084568
      *                    ROTINA DE SAIDA - F3                   *     00084668
      *************************************************************     00084768
                                                                        00084868
           IF EIBAID = '3'                                              00084968
              MOVE ' ' TO WRK-SAIDA                                     00085068
              EXEC CICS SEND TEXT                                       00086068
               FROM(WRK-SAIDA)                                          00086168
               ERASE                                                    00086268
              END-EXEC                                                  00086368
                                                                        00086468
              EXEC CICS                                                 00086568
                RETURN                                                  00086668
              END-EXEC                                                  00086768
                                                                        00086868
           END-IF.                                                      00086968
                                                                        00087068
      *************************************************************     00088068
      *                  ROTINA DE CONSULTA - F6                  *     00089068
      *************************************************************     00090068
                                                                        00091068
           IF EIBAID = '6'                                              00091168
                                                                        00091268
              MOVE IDI                             TO WRK-CHAVE         00091368
                                                                        00091468
               EXEC CICS LINK                                           00091568
                PROGRAM('F10COVSA')                                     00091669
                COMMAREA(WRK-DADOS)                                     00091768
                RESP(WRK-RET-CICS)                                      00091868
               END-EXEC                                                 00091968
                                                                        00092068
               EVALUATE WRK-STATUS                                      00093068
                 WHEN '1'                                               00094068
                   MOVE WRK-CHAVE                    TO IDO             00095068
                   MOVE WRK-NOME                     TO NOMEO           00095168
                   MOVE WRK-SETOR                    TO SETORO          00095268
                   MOVE WRK-SALARIO                  TO SALO            00095370
                   MOVE WRK-DATAADM                  TO DATADMO         00095468
                   MOVE WRK-EMAIL                    TO EMAILO          00095568
                    MOVE 'ENCONTRADO'                TO MSGO            00095668
                 WHEN '2'                                               00095768
                    MOVE 'NAO ENCONTRADO'            TO MSGO            00095868
                                                                        00095968
                 WHEN '3'                                               00096068
                    MOVE 'SALARIO COM ERRO'          TO MSGO            00096168
                                                                        00096268
                 WHEN '4'                                               00096368
                    MOVE 'EMAIL COM ERRO'            TO MSGO            00096468
               END-EVALUATE                                             00096568
                                                                        00096668
           END-IF.                                                      00096768
                                                                        00096868
      *************************************************************     00096968
      *                    ROTINA DE ENTER                        *     00097068
      *************************************************************     00097168
                                                                        00097268
           IF EIBAID EQUAL ''''                                         00097368
           INITIALIZE MAPTESTO                                          00097472
      *       MOVE SPACES TO NOMEO SETORO EMAILO DATADMO                00097572
      *       MOVE ZEROS  TO IDO SALO                                   00097672
              MOVE 'NOVA PESQUISA  ' TO MSGO                            00097768
           END-IF.                                                      00097868
                                                                        00097968
      *---------------------------------------------------------*       00098068
       2000-99-FIM.                     EXIT.                           00098168
      *---------------------------------------------------------*       00099068
                                                                        00100068
      *---------------------------------------------------------*       00110068
       3000-FINALIZAR                            SECTION.               00111068
      *---------------------------------------------------------*       00112068
                                                                        00113068
           IF WRK-FLAG EQUAL 1                                          00113168
               EXEC CICS SEND                                           00113268
               MAPSET('F10CIM1')                                        00113369
                MAP('MAPTEST')                                          00113468
                ERASE                                                   00113568
                MAPONLY                                                 00113668
               END-EXEC                                                 00113768
            ELSE                                                        00113868
               EXEC CICS SEND                                           00113968
                MAPSET('F10CIM1')                                       00114069
                MAP('MAPTEST')                                          00114168
                DATAONLY                                                00114268
               END-EXEC                                                 00114368
            END-IF.                                                     00114468
                                                                        00114568
      *---------------------------------------------------------*       00114668
       3000-99-FIM.        EXIT.                                        00114768
      *---------------------------------------------------------*       00114868
                                                                        00114968
