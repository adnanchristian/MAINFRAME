      *================================================================*00001000
       IDENTIFICATION                            DIVISION.              00002000
      *================================================================*00002100
                                                                        00002200
       PROGRAM-ID.  FR10RELA.                                           00002300
       AUTHOR.      ADNAN.                                              00002400
                                                                        00002500
      *================================================================*00002600
      *                         F O U R S Y S                          *00002700
      *================================================================*00002800
      *    PROGRAMA...: FR10RELA                                       *00002900
      *    PROGAMADOR.: ADNAN CHRISTIAN                                *00003000
      *    EMPRESA....: FOURSYS                                        *00003100
      *    ANALISTA...: IVAN SANCHES                                   *00003200
      *    DATA.......: 27/05/2022                                     *00003300
      *----------------------------------------------------------------*00003400
      *    OBJETIVO : ESTE PROGRAMA TEM A FINALIDADE DE GERAR RELATORIO*00003500
      *               DO ARQUIVO MOV2505A                              *00003800
      *----------------------------------------------------------------*00003900
      *    ARQUIVOS....:                                               *00004000
      *      DDNAME                I/O             INCLUDE/BOOK        *00004100
      *     MOV2505A                I              ------------        *00004400
      *     RELATORI                O              ------------        *00004500
      *----------------------------------------------------------------*00004600
      *    MODULOS....:                                                *00004700
      *    GRAVALOG - TRATAMENTO DE ERROS                              *00004800
      *                                                                *00004900
      ******************************************************************00005000
      *================================================================*00005100
                                                                        00005200
      *================================================================*00005300
       ENVIRONMENT DIVISION.                                            00005400
      *================================================================*00005500
                                                                        00005600
      *----------------------------------------------------------------*00005700
       CONFIGURATION                              SECTION.              00005800
      *----------------------------------------------------------------*00005900
                                                                        00006000
       SPECIAL-NAMES.                                                   00006100
           DECIMAL-POINT IS COMMA.                                      00006200
                                                                        00006300
      *----------------------------------------------------------------*00006400
                                                                        00006500
      *----------------------------------------------------------------*00006600
       INPUT-OUTPUT                               SECTION.              00006700
      *----------------------------------------------------------------*00006800
                                                                        00006900
       FILE-CONTROL.                                                    00007000
                                                                        00007600
             SELECT MOV2505A ASSIGN     TO MOV2505A                     00007700
                    FILE STATUS         IS WRK-FS-MOV2505A.             00007800
                                                                        00007900
             SELECT RELATORI ASSIGN     TO RELATORI                     00008000
                    FILE STATUS         IS WRK-FS-RELATORI.             00008100
                                                                        00008200
                                                                        00008300
      *================================================================*00008400
       DATA                                      DIVISION.              00008500
      *================================================================*00008600
                                                                        00008700
      *----------------------------------------------------------------*00008800
       FILE                                      SECTION.               00008900
      *----------------------------------------------------------------*00009000
                                                                        00013400
      *----------------------------------------------------------------*00013500
      *                                                                *00013600
      *   INPUT - DADOS DO ARQUIVO DE ENTRADA (MOV2505A)               *00013700
      *                                                  - LRECL = 046 *00013800
      *----------------------------------------------------------------*00013900
                                                                        00014000
       FD MOV2505A                                                      00014100
           RECORDING MODE IS F                                          00014200
           LABEL RECORD IS STANDARD                                     00014300
           BLOCK CONTAINS 0 RECORDS.                                    00014400
                                                                        00014500
       01 FD-MOV2505A.                                                  00014600
          05 FD-MOV2505A-CHAVE.                                         00014700
             10 FD-MOV2505A-AGENCIA      PIC 9(004).                    00014800
             10 FD-MOV2505A-CONTA        PIC 9(004).                    00014900
          05 FD-MOV2505A-NOME            PIC X(030).                    00015000
          05 FD-MOV2505A-SALDO           PIC 9(008).                    00015100
                                                                        00015200
      *----------------------------------------------------------------*00015300
      *                                                                *00015400
      *   INPUT - DADOS DO ARQUIVO DE SAIDA (RELATORI)                 *00015500
      *                                                  - LRECL = 064 *00015655
      *----------------------------------------------------------------*00015700
                                                                        00015800
       FD RELATORI                                                      00015900
           RECORDING MODE IS F                                          00016000
           LABEL RECORD IS STANDARD                                     00016100
           BLOCK CONTAINS 0 RECORDS.                                    00016200
                                                                        00016300
       01 FD-RELATORI         PIC X(64).                                00016555
                                                                        00017828
      *----------------------------------------------------------------*00017901
       WORKING-STORAGE                            SECTION.              00018001
      *----------------------------------------------------------------*00018101
                                                                        00018201
      *----------------------------------------------------------------*00018301
       01  FILLER                      PIC  X(050)         VALUE        00018401
           '*** INICIO DA WORKING FR10EX03 ***'.                        00018501
      *----------------------------------------------------------------*00018601
                                                                        00018701
                                                                        00019000
      *----------------------------------------------------------------*00020000
       01  FILLER                       PIC X(050)          VALUE       00020100
           '*** TESTE DE FILE STATUS ***'.                              00020200
      *----------------------------------------------------------------*00020300
                                                                        00020400
        77 WRK-FS-RELATORI       PIC 9(002).                            00020607
        77 WRK-FS-MOV2505A       PIC 9(002).                            00020700
                                                                        00020900
      *----------------------------------------------------------------*00021009
       01  FILLER                       PIC X(050)           VALUE      00021109
           '*** AREA DE AUXILIARES ***'.                                00021214
      *----------------------------------------------------------------*00021309
                                                                        00021409
        77 WRK-AUX-PAG              PIC 9(003).                         00021509
        77 WRK-AUX-RES              PIC 9(003).                         00021610
                                                                        00021810
      *----------------------------------------------------------------*00021910
       01  FILLER                       PIC X(050)           VALUE      00022010
           '*** AREA DE CABECALHOS ***'.                                00022110
      *----------------------------------------------------------------*00022210
                                                                        00022310
        01 WRK-CAB01.                                                   00022410
           05 FILLER               PIC X(058) VALUE                     00022552
              '*            RELATORIO DE CLIENTES                PAG: '.00022653
           05 WRK-PAG              PIC 9(002) VALUE ZEROS.              00022710
           05 FILLER               PIC X(004) VALUE SPACES.             00022852
                                                                        00022910
        01 WRK-CAB02.                                                   00023010
           05 FILLER               PIC X(007) VALUE 'AGENCIA'.          00023110
           05 FILLER               PIC X(006) VALUE '/CONTA'.           00023224
           05 FILLER               PIC X(007) VALUE SPACES.             00023311
           05 FILLER               PIC X(004) VALUE 'NOME'.             00023413
           05 FILLER               PIC X(029) VALUE SPACES.             00023524
           05 FILLER               PIC X(011) VALUE 'SALDO ATUAL'.      00023613
                                                                        00023810
        01 WRK-TRACE               PIC X(64) VALUE ALL '-'.             00023931
                                                                        00024032
      *----------------------------------------------------------------*00024332
       01  FILLER                       PIC X(050)           VALUE      00024432
           '*** AREA DE ACUMULADORES ***'.                              00024532
      *----------------------------------------------------------------*00024632
                                                                        00024732
        77 ACUM-GRAVS-RELATORI      PIC 9(004).                         00024832
        77 ACUM-LIDOS-MOV2505A      PIC 9(004).                         00024932
        77 ACUM-LINHAS              PIC 9(002).                         00025032
                                                                        00025100
      *----------------------------------------------------------------*00026000
       01  FILLER                       PIC X(050)           VALUE      00027000
           '*** AREA DA GRAVLOG ***'.                                   00028000
      *----------------------------------------------------------------*00029000
                                                                        00030000
        COPY 'B#GRVLOG'.                                                00040000
                                                                        00040100
      *----------------------------------------------------------------*00040256
       01  FILLER                       PIC X(050)           VALUE      00040356
           '*** AREA DA RELATORI ***'.                                  00040456
      *----------------------------------------------------------------*00040556
                                                                        00040656
        COPY 'B#RELA'.                                                  00040756
                                                                        00040856
      *----------------------------------------------------------------*00040956
       01  FILLER                      PIC  X(050)         VALUE        00041056
           '*** FR10EX03 - FIM DA AREA DE WORKING ***'.                 00041156
      *----------------------------------------------------------------*00041256
                                                                        00041356
      *================================================================*00041456
        PROCEDURE                       DIVISION.                       00041556
      *================================================================*00041656
                                                                        00041756
                                                                        00041856
      ******************************************************************00041956
      *                    ROTINA PRINCIPAL                           * 00042056
      ******************************************************************00042156
                                                                        00042256
      *----------------------------------------------------------------*00042356
       0000-PRINCIPAL                            SECTION.               00042456
      *----------------------------------------------------------------*00042556
                                                                        00042656
           PERFORM 1000-INICIAR.                                        00042756
                                                                        00042856
           PERFORM 2000-VERIFICAR-VAZIO.                                00042956
                                                                        00043056
           PERFORM 3000-PROCESSAR UNTIL WRK-FS-MOV2505A  EQUAL '10'     00043156
                                                                        00043256
           PERFORM 4000-FINALIZAR.                                      00043356
                                                                        00043456
           STOP RUN.                                                    00043556
                                                                        00043656
      *----------------------------------------------------------------*00043756
       0000-99-FIM.                           EXIT.                     00043856
      *----------------------------------------------------------------*00043956
                                                                        00044056
      ******************************************************************00044156
      *                    PROCEDIMENTOS INICIAIS                     * 00044256
      ******************************************************************00044356
                                                                        00044456
      *----------------------------------------------------------------*00044556
       1000-INICIAR                           SECTION.                  00044656
      *----------------------------------------------------------------*00044756
                                                                        00044856
            OPEN INPUT MOV2505A                                         00044956
                 OUTPUT RELATORI.                                       00045056
                                                                        00045156
            PERFORM 1100-TESTAR-FILE-STATUS.                            00045256
                                                                        00045356
      *----------------------------------------------------------------*00045456
       1000-99-FIM.                              EXIT.                  00045556
      *----------------------------------------------------------------*00045656
                                                                        00045756
      ******************************************************************00045856
      *               TESTE DE FILE-STATUS                             *00045956
      ******************************************************************00046056
                                                                        00046156
      *----------------------------------------------------------------*00046256
       1100-TESTAR-FILE-STATUS                   SECTION.               00046356
      *----------------------------------------------------------------*00046456
                                                                        00046556
            PERFORM 1110-TESTAR-FS-MOV2505A.                            00046656
                                                                        00046756
            PERFORM 1120-TESTAR-FS-RELATORI.                            00046856
                                                                        00046956
      *----------------------------------------------------------------*00047000
       1100-99-FIM.                               EXIT.                 00047100
      *----------------------------------------------------------------*00047200
                                                                        00047300
      ******************************************************************00047400
      *    TESTAR FILE-STATUS DO ARQUIVO DE ENTRADA - MOV2505A         *00047503
      ******************************************************************00047600
                                                                        00047700
      *----------------------------------------------------------------*00047800
       1110-TESTAR-FS-MOV2505A                SECTION.                  00047903
      *----------------------------------------------------------------*00048000
                                                                        00048100
           IF WRK-FS-MOV2505A NOT EQUAL ZEROS                           00048203
              MOVE 'FR10RELA'                 TO WRK-PROGRAMA           00048331
              MOVE 'ERRO NO OPEN CLI2505A'    TO WRK-MENSAGEM           00048431
              MOVE '1000'                     TO WRK-SECAO              00048531
              MOVE WRK-FS-MOV2505A            TO WRK-STATUS             00048631
              PERFORM 9999-TRATAR-ERRO                                  00048731
                                                                        00048800
           END-IF.                                                      00048900
                                                                        00049000
      *----------------------------------------------------------------*00050000
       1110-99-FIM.                            EXIT.                    00050100
      *----------------------------------------------------------------*00050200
                                                                        00050300
      ******************************************************************00050400
      *    TESTAR FILE-STATUS DO ARQUIVO DE SAIDA - REALTORI           *00050503
      ******************************************************************00050600
                                                                        00050700
      *----------------------------------------------------------------*00050800
       1120-TESTAR-FS-RELATORI               SECTION.                   00050908
      *----------------------------------------------------------------*00051000
                                                                        00051100
           IF WRK-FS-RELATORI NOT EQUAL ZEROS                           00051203
              MOVE 'FR10RELA'                TO WRK-PROGRAMA            00051331
              MOVE 'ERRO NO OPEN RELATORI'   TO WRK-MENSAGEM            00051431
              MOVE '1000'                    TO WRK-SECAO               00051531
              MOVE WRK-FS-RELATORI           TO WRK-STATUS              00051631
                                                                        00051700
              PERFORM 9999-TRATAR-ERRO                                  00051831
                                                                        00051900
           END-IF.                                                      00052000
                                                                        00052100
      *----------------------------------------------------------------*00052200
       1120-99-FIM.                            EXIT.                    00052300
      *----------------------------------------------------------------*00052400
      *----------------------------------------------------------------*00056600
                                                                        00056700
      ******************************************************************00056800
      *                   VERIFICAR VAZIO                              *00056900
      ******************************************************************00057000
                                                                        00058000
      *----------------------------------------------------------------*00059000
       2000-VERIFICAR-VAZIO                 SECTION.                    00059100
      *----------------------------------------------------------------*00059200
                                                                        00059300
           PERFORM 2100-LER-MOV2505A.                                   00059403
                                                                        00059500
      *----------------------------------------------------------------*00059800
       2000-99-FIM.                            EXIT.                    00059900
      *----------------------------------------------------------------*00060000
                                                                        00060100
      ******************************************************************00060200
      *   LEITURA DO ARQUIVO DE ENTRADA - MOV2505A                     *00060303
      ******************************************************************00060400
                                                                        00060500
      *----------------------------------------------------------------*00060600
       2100-LER-MOV2505A                         SECTION.               00060703
      *----------------------------------------------------------------*00060800
                                                                        00060900
           READ MOV2505A.                                               00061006
            ADD 1 TO ACUM-LIDOS-MOV2505A.                               00061131
                                                                        00061200
      *----------------------------------------------------------------*00061300
       2100-99-FIM.                              EXIT.                  00061400
      *----------------------------------------------------------------*00061500
                                                                        00063400
      ******************************************************************00063500
      *                   PROCESSAMENTO PRINCIPAL                      *00063600
      ******************************************************************00063700
                                                                        00063800
      *----------------------------------------------------------------*00063900
       3000-PROCESSAR                       SECTION.                    00064000
      *----------------------------------------------------------------*00064100
                                                                        00064200
           PERFORM 3100-GERAR-RELATORIO.                                00064303
                                                                        00064400
      *----------------------------------------------------------------*00064500
       3000-99-FIM.                            EXIT.                    00064600
      *----------------------------------------------------------------*00064700
                                                                        00064800
      *----------------------------------------------------------------*00064900
                                                                        00065000
      ******************************************************************00065100
      *   GERAR RELATORIO                                              *00065203
      ******************************************************************00065300
                                                                        00065400
      *----------------------------------------------------------------*00065500
       3100-GERAR-RELATORIO                SECTION.                     00065603
      *----------------------------------------------------------------*00065700
                                                                        00065809
           INITIALIZE REG-RELATORI.                                     00065955
                                                                        00066009
           MOVE FD-MOV2505A-CHAVE        TO REL-CHAVE                   00066355
           MOVE FD-MOV2505A-NOME         TO REL-NOME                    00066455
           MOVE FD-MOV2505A-SALDO        TO REL-SALDO                   00067055
           PERFORM 3110-IMPRIMIR-CABECALHO.                             00068010
                                                                        00068149
           WRITE FD-RELATORI FROM REG-RELATORI.                         00068257
           ADD 1                         TO ACUM-GRAVS-RELATORI.        00068349
           ADD 1                         TO ACUM-LINHAS.                00068449
           PERFORM 2100-LER-MOV2505A.                                   00069050
                                                                        00072003
      *----------------------------------------------------------------*00073005
       3100-99-FIM.                           EXIT.                     00074005
      *----------------------------------------------------------------*00074105
      *----------------------------------------------------------------*00074205
                                                                        00074305
      ******************************************************************00074405
      *   IMPRIMIR O CABECALHO DO RELATORIO                            *00074510
      ******************************************************************00074605
                                                                        00074705
      *----------------------------------------------------------------*00074805
       3110-IMPRIMIR-CABECALHO              SECTION.                    00074910
      *----------------------------------------------------------------*00075005
                                                                        00075105
           DIVIDE ACUM-LINHAS BY 5 GIVING WRK-AUX-RES                   00075210
           REMAINDER WRK-AUX-PAG.                                       00075330
                                                                        00075430
           IF WRK-AUX-PAG EQUAL ZEROS                                   00075509
              PERFORM 3120-GRAVAR-RELATORIO                             00075617
           END-IF.                                                      00076816
                                                                        00076916
      *----------------------------------------------------------------*00077005
       3110-99-FIM.                           EXIT.                     00077110
      *----------------------------------------------------------------*00077205
                                                                        00077310
      ******************************************************************00077410
      *   GRAVAR RELATORIO                                             *00077510
      ******************************************************************00077610
                                                                        00077710
      *----------------------------------------------------------------*00077810
       3120-GRAVAR-RELATORIO                SECTION.                    00077910
      *----------------------------------------------------------------*00078010
                                                                        00078110
           IF WRK-AUX-PAG EQUAL ZEROS                                   00078310
              ADD 1                         TO WRK-PAG                  00078416
              WRITE FD-RELATORI             FROM WRK-TRACE              00078639
              WRITE FD-RELATORI             FROM WRK-CAB01 AFTER PAGE   00078839
              WRITE FD-RELATORI             FROM WRK-TRACE              00078954
              WRITE FD-RELATORI             FROM WRK-CAB02              00079043
              WRITE FD-RELATORI             FROM WRK-TRACE              00079150
                                                                        00079210
           END-IF.                                                      00079310
                                                                        00079410
      *----------------------------------------------------------------*00079510
       3120-99-FIM.                           EXIT.                     00079610
      *----------------------------------------------------------------*00079710
                                                                        00079810
      *----------------------------------------------------------------*00079910
                                                                        00080010
      ******************************************************************00080110
      *                       FINALIZACAO                              *00080210
      ******************************************************************00080310
                                                                        00080410
      *----------------------------------------------------------------*00080510
       4000-FINALIZAR                        SECTION.                   00080610
      *----------------------------------------------------------------*00080710
                                                                        00080810
           CLOSE MOV2505A                                               00080910
                 RELATORI.                                              00081010
                                                                        00081110
           PERFORM 4100-EMITIR-TOTAIS.                                  00081210
                                                                        00081310
      *----------------------------------------------------------------*00081410
       4000-99-FIM.                           EXIT.                     00081510
      *----------------------------------------------------------------*00081610
                                                                        00081710
      ******************************************************************00081810
      *                       EMITIR TOTAIS                            *00081910
      ******************************************************************00082010
                                                                        00082110
      *----------------------------------------------------------------*00082210
       4100-EMITIR-TOTAIS                    SECTION.                   00082310
      *----------------------------------------------------------------*00082410
                                                                        00082510
           DISPLAY '****************** FR10RELA **********************'.00082610
           DISPLAY '*                                                *'.00082710
           DISPLAY '*                                                *'.00082810
           DISPLAY '---------------- REGISTROS LIDOS -----------------'.00082910
           DISPLAY '*                                                *'.00083010
           DISPLAY '*  TOTAIS LIDOS MOV2505A: ' ACUM-LIDOS-MOV2505A.    00083110
           DISPLAY '*                                                *'.00083210
           DISPLAY '*                                                *'.00083310
           DISPLAY '---------------- REGISTROS GRAVADOS --------------'.00083410
           DISPLAY '*                                                *'.00083510
           DISPLAY '*                                                *'.00083610
           DISPLAY '*  TOTAIS GRAVADOS RELATORI: ' ACUM-GRAVS-RELATORI. 00083710
           DISPLAY '*                                                *'.00083810
           DISPLAY '*                                                *'.00083910
           DISPLAY '****************** FR10RELA **********************'.00084010
                                                                        00084110
      *----------------------------------------------------------------*00084210
       4100-99-FIM.                           EXIT.                     00084310
      *----------------------------------------------------------------*00084410
                                                                        00084510
      ******************************************************************00084610
      *                       TRATAMENTO DE ERRO                       *00084710
      ******************************************************************00084810
                                                                        00084910
      *----------------------------------------------------------------*00085010
       9999-TRATAR-ERRO                      SECTION.                   00085110
      *----------------------------------------------------------------*00085210
                                                                        00085310
           CALL 'GRAVALOG'         USING WRK-LOG.                       00085410
                                                                        00085510
           GOBACK.                                                      00085610
                                                                        00085710
      *----------------------------------------------------------------*00085810
       9999-99-FIM.                           EXIT.                     00085910
      *----------------------------------------------------------------*00086010
