      *================================================================*00001000
       IDENTIFICATION                            DIVISION.              00002000
      *================================================================*00002100
                                                                        00002200
       PROGRAM-ID.  MD021004.                                           00002300
       AUTHOR.      ADNAN.                                              00002400
                                                                        00002500
      *================================================================*00002600
      *                         F O U R S Y S                          *00002700
      *================================================================*00002800
      *    PROGRAMA   : MD021004                                       *00002900
      *    PROGAMADOR : ADNAN CHRISTIAN                                *00003000
      *    EMPRESA    : FOURSYS                                        *00003100
      *    ANALISTA   : IVAN SANCHES                                   *00003200
      *    DATA       : 13/07/2022                                     *00003335
      *----------------------------------------------------------------*00003400
      *    OBJETIVO : REALIZAR REWRITE NO ARQUIVO VSAM EVSA0407 E GRA_ *00003536
      *               VAR A EXCECAO NO ARQUIVO EXC1207                 *00003636
      *----------------------------------------------------------------*00003736
      *    ARQUIVOS :                                                  *00003836
      *      DDNAME                I/O             INCLUDE/BOOK        *00003936
      *     EVSA0407                O              ------------        *00004026
      *     MOV1207                 I              ------------        *00004126
      *     EXC1207                 O              ------------        *00004226
      *----------------------------------------------------------------*00004321
      *    MODULOS    :                                                *00004421
      *    --------                                                    *00004537
      *----------------------------------------------------------------*00004737
      *    COPYBOOKS  :                                                *00004837
      *    B#GLOB - TRATAMENTO DE ERROS                                *00004937
      *                                                                *00005038
      ******************************************************************00005137
      *================================================================*00005237
                                                                        00005337
      *================================================================*00005437
       ENVIRONMENT DIVISION.                                            00005537
      *================================================================*00005637
                                                                        00005737
      *----------------------------------------------------------------*00005837
       CONFIGURATION                              SECTION.              00005937
      *----------------------------------------------------------------*00006037
                                                                        00006137
       SPECIAL-NAMES.                                                   00006237
           DECIMAL-POINT IS COMMA.                                      00006337
                                                                        00006437
      *----------------------------------------------------------------*00006537
       INPUT-OUTPUT                               SECTION.              00006637
      *----------------------------------------------------------------*00006737
                                                                        00006837
       FILE-CONTROL.                                                    00006937
            SELECT EVSA0407            ASSIGN TO EVSA0407               00007037
               ORGANIZATION            IS INDEXED                       00007137
               ACCESS MODE             IS DYNAMIC                       00007237
               RECORD KEY              IS FD-CHAVE                      00007337
               FILE STATUS             IS WRK-FS-EVSA0407.              00007437
                                                                        00007537
            SELECT MOV1207             ASSIGN TO MOV1207                00007637
               FILE STATUS             IS WRK-FS-MOV1207.               00007737
                                                                        00007837
            SELECT EXC1207             ASSIGN TO EXC1207                00007937
               FILE STATUS             IS WRK-FS-EXC1207.               00008037
                                                                        00008137
      *================================================================*00008237
       DATA                                      DIVISION.              00008337
      *================================================================*00008437
                                                                        00008537
      *----------------------------------------------------------------*00008637
       FILE                                      SECTION.               00008737
      *----------------------------------------------------------------*00008837
                                                                        00008937
      *----------------------------------------------------------------*00009037
      *   INPUT - DADOS DO ARQUIVO DE SAIDA(EVSA0407)                  *00009137
      *                               - LRECL = 019                    *00009237
      *----------------------------------------------------------------*00009337
                                                                        00009437
       FD EVSA0407.                                                     00009537
       01 FD-EVSA0407.                                                  00009637
          05 FD-CHAVE.                                                  00009737
             10 FD-AGENCIA              PIC X(004).                     00009837
             10 FD-CONTA                PIC X(005).                     00009937
          05 FD-SALARIO                 PIC X(010).                     00010037
                                                                        00010137
      *----------------------------------------------------------------*00010237
      *   INPUT - DADOS DO ARQUIVO DE ENTRADA(MOV1207)                 *00010336
      *                               - LRECL = 019                    *00010426
      *----------------------------------------------------------------*00010521
                                                                        00010621
       FD MOV1207                                                       00010721
           RECORDING MODE IS F                                          00010821
           BLOCK CONTAINS 0 RECORDS.                                    00010921
                                                                        00011021
       01 FD-MOV1207.                                                   00011121
          05 FD-MOV1207-AGENCIA         PIC X(004).                     00011221
          05 FD-MOV1207-CONTA           PIC X(005).                     00011321
          05 FD-MOV1207-SALARIO         PIC X(010).                     00011421
                                                                        00011521
      *----------------------------------------------------------------*00011621
      *   INPUT - DADOS DO ARQUIVO DE SAIDA(EXC1207)                   *00011826
      *                               - LRECL = 019                    *00011926
      *----------------------------------------------------------------*00012021
                                                                        00012126
       FD EXC1207                                                       00012221
           RECORDING MODE IS F                                          00012321
           BLOCK CONTAINS 0 RECORDS.                                    00012421
                                                                        00012521
       01 FD-EXC1207           PIC X(019).                              00012621
                                                                        00012721
      *----------------------------------------------------------------*00012821
       WORKING-STORAGE                            SECTION.              00012900
      *----------------------------------------------------------------*00013000
                                                                        00014000
      *----------------------------------------------------------------*00015000
       01  FILLER                      PIC  X(050)         VALUE        00016000
           '*** INICIO DA WORKING MD021004 ***'.                        00017016
      *----------------------------------------------------------------*00018000
                                                                        00018125
      *----------------------------------------------------------------*00018226
       01  FILLER                       PIC X(050)           VALUE      00018326
           '*** AREA DE ACUMULADORES ***'.                              00018426
      *----------------------------------------------------------------*00018526
                                                                        00018626
        77 ACUM-LIDOS-MOV1207       PIC 9(009) COMP-3  VALUE ZEROS.     00018726
        77 ACUM-ATT-MOV1207         PIC 9(009) COMP-3  VALUE ZEROS.     00018826
        77 ACUM-INCONS              PIC 9(009) COMP-3  VALUE ZEROS.     00018926
                                                                        00019026
      *----------------------------------------------------------------*00019126
       01  FILLER                       PIC X(050)          VALUE       00019226
           '*** TESTE DE FILE STATUS ***'.                              00019326
      *----------------------------------------------------------------*00019426
                                                                        00019526
        77 WRK-FS-EVSA0407       PIC X(002)      VALUE SPACES.          00019626
        77 WRK-FS-MOV1207        PIC X(002)      VALUE SPACES.          00019726
        77 WRK-FS-EXC1207        PIC X(002)      VALUE SPACES.          00019826
        77 WRK-FS-OPERACAO       PIC X(013)      VALUE SPACES.          00019926
        77 WRK-FS-LOCAL          PIC X(004)      VALUE SPACES.          00020026
                                                                        00020126
      *----------------------------------------------------------------*00020225
       01  FILLER                      PIC  X(050)         VALUE        00020325
           '*** AREA DO ARQUIVO - EVSA0407 ***'.                        00020425
      *----------------------------------------------------------------*00020525
                                                                        00020625
        01 WRK-EVSA0407.                                                00020725
           05 WRK-CHAVE.                                                00020825
              10 WRK-AGENCIA           PIC X(04)  VALUE SPACES.         00020935
              10 WRK-CONTA             PIC X(05)  VALUE SPACES.         00021035
           05 WRK-SALARIO              PIC X(10)  VALUE SPACES.         00021135
                                                                        00021225
      *----------------------------------------------------------------*00021425
       01  FILLER                       PIC X(050)           VALUE      00021525
           '*** AREA PARA AUXILIARES ***'.                              00021626
      *----------------------------------------------------------------*00021725
                                                                        00021825
        77 WRK-INCONS             PIC X(001)  VALUE SPACES.             00022135
                                                                        00022225
      *----------------------------------------------------------------*00022325
       01  FILLER                       PIC X(050)           VALUE      00022425
           '*** AREA DA B#GLOB ***'.                                    00022526
      *----------------------------------------------------------------*00022625
                                                                        00022725
        COPY 'B#GLOB'.                                                  00022825
                                                                        00022925
      *----------------------------------------------------------------*00023025
       01  FILLER                       PIC  X(050)         VALUE       00023125
           '*** MD021004 - FIM DA AREA DE WORKING ***'.                 00023225
      *----------------------------------------------------------------*00023325
                                                                        00024025
      *================================================================*00040600
        PROCEDURE               DIVISION.                               00040704
      *================================================================*00040800
                                                                        00040900
      ******************************************************************00041000
      *                    ROTINA PRINCIPAL                           * 00041100
      ******************************************************************00041200
                                                                        00041300
      *----------------------------------------------------------------*00041400
       0000-PRINCIPAL                            SECTION.               00041500
      *----------------------------------------------------------------*00041600
                                                                        00041700
           PERFORM 1000-INICIAR.                                        00041800
                                                                        00041900
           PERFORM 2000-VERIFICAR-VAZIO.                                00042000
                                                                        00043000
           PERFORM 3000-PROCESSAR                                       00043126
               UNTIL WRK-FS-MOV1207 EQUAL '10'                          00043226
                                                                        00043326
           PERFORM 4000-FINALIZAR.                                      00043400
                                                                        00043500
           STOP RUN.                                                    00043612
                                                                        00043712
      *----------------------------------------------------------------*00043800
       0000-99-FIM.                           EXIT.                     00043900
      *----------------------------------------------------------------*00044000
                                                                        00044100
      ******************************************************************00044200
      *                    PROCEDIMENTOS INICIAIS                     * 00044300
      ******************************************************************00044400
                                                                        00044500
      *----------------------------------------------------------------*00044600
       1000-INICIAR                           SECTION.                  00044700
      *----------------------------------------------------------------*00044800
                                                                        00044900
            OPEN I-O    EVSA0407                                        00045001
                 INPUT  MOV1207                                         00045121
                 OUTPUT EXC1207.                                        00045221
                                                                        00045321
            MOVE WRK-ABERTURA       TO WRK-FS-OPERACAO.                 00045400
            MOVE '1000'             TO WRK-FS-LOCAL.                    00045500
                                                                        00045601
            PERFORM 1100-FILE-STATUS.                                   00045700
                                                                        00045800
      *----------------------------------------------------------------*00045900
       1000-99-FIM.                              EXIT.                  00046000
      *----------------------------------------------------------------*00046100
                                                                        00046200
      ******************************************************************00046300
      *               TESTE DE FILE-STATUS                             *00046400
      ******************************************************************00046500
                                                                        00046600
      *----------------------------------------------------------------*00046700
       1100-FILE-STATUS                          SECTION.               00046800
      *----------------------------------------------------------------*00046900
                                                                        00047000
            PERFORM 1110-TESTAR-FS-EVSA0407.                            00047100
                                                                        00047200
            PERFORM 1120-TESTAR-FS-MOV1207.                             00047300
                                                                        00047400
            PERFORM 1130-TESTAR-FS-EXC1207.                             00047521
                                                                        00047621
      *----------------------------------------------------------------*00047721
       1100-99-FIM.                               EXIT.                 00047821
      *----------------------------------------------------------------*00047921
                                                                        00048021
      ******************************************************************00048121
      *    TESTAR FILE-STATUS DO ARQUIVO DE SAIDA - EVSA0407           *00048236
      ******************************************************************00048321
                                                                        00048421
      *----------------------------------------------------------------*00048521
       1110-TESTAR-FS-EVSA0407                SECTION.                  00048621
      *----------------------------------------------------------------*00048721
                                                                        00048821
           IF WRK-FS-EVSA0407 NOT EQUAL ZEROS                           00048921
              MOVE 'MD021004'                   TO WRK-PROGRAMA         00049021
              MOVE WRK-FS-EVSA0407              TO WRK-STATUS           00049121
              MOVE WRK-FS-OPERACAO              TO WRK-MENSAGEM         00049221
                                                OF WRK-LOG              00049321
              MOVE WRK-FS-LOCAL                 TO WRK-SECAO            00049421
              PERFORM 9999-TRATAR-ERRO                                  00049521
           END-IF.                                                      00049621
                                                                        00049721
      *----------------------------------------------------------------*00049821
       1110-99-FIM.                            EXIT.                    00049921
      *----------------------------------------------------------------*00050021
                                                                        00050121
      ******************************************************************00050221
      *    TESTAR FILE-STATUS DO ARQUIVO DE ENTRADA - MOV1207          *00050336
      ******************************************************************00050421
                                                                        00050521
      *----------------------------------------------------------------*00050621
       1120-TESTAR-FS-MOV1207                 SECTION.                  00050721
      *----------------------------------------------------------------*00050821
                                                                        00050921
           IF WRK-FS-MOV1207 NOT EQUAL ZEROS                            00051021
              MOVE 'MD021004'                   TO WRK-PROGRAMA         00051121
              MOVE WRK-FS-MOV1207               TO WRK-STATUS           00051221
              MOVE WRK-FS-OPERACAO              TO WRK-MENSAGEM         00051321
                                                OF WRK-LOG              00051421
              MOVE WRK-FS-LOCAL                 TO WRK-SECAO            00051521
              PERFORM 9999-TRATAR-ERRO                                  00051621
           END-IF.                                                      00051721
                                                                        00051821
      *----------------------------------------------------------------*00051921
       1120-99-FIM.                            EXIT.                    00052021
      *----------------------------------------------------------------*00052121
                                                                        00052221
      ******************************************************************00052321
      *    TESTAR FILE-STATUS DO ARQUIVO DE SAIDA - EXC1207            *00052421
      ******************************************************************00052521
                                                                        00052621
      *----------------------------------------------------------------*00052721
       1130-TESTAR-FS-EXC1207                 SECTION.                  00052821
      *----------------------------------------------------------------*00052921
                                                                        00053021
           IF WRK-FS-MOV1207 NOT EQUAL ZEROS                            00053121
              MOVE 'MD021004'                   TO WRK-PROGRAMA         00053221
              MOVE WRK-FS-MOV1207               TO WRK-STATUS           00053321
              MOVE WRK-FS-OPERACAO              TO WRK-MENSAGEM         00053421
                                                OF WRK-LOG              00053521
              MOVE WRK-FS-LOCAL                 TO WRK-SECAO            00053621
              PERFORM 9999-TRATAR-ERRO                                  00053721
           END-IF.                                                      00053821
                                                                        00053921
      *----------------------------------------------------------------*00054021
       1130-99-FIM.                            EXIT.                    00054121
      *----------------------------------------------------------------*00054221
                                                                        00054324
      ******************************************************************00054424
      *          VERIFICAR ARQUIVO VAZIO                               *00054526
      ******************************************************************00054624
                                                                        00054724
      *----------------------------------------------------------------*00054821
       2000-VERIFICAR-VAZIO                 SECTION.                    00054921
      *----------------------------------------------------------------*00055021
                                                                        00055121
           READ MOV1207                                                 00055221
                                                                        00055321
           IF WRK-FS-MOV1207                EQUAL '10'                  00055426
              DISPLAY '***************** EVSA0407 *****************'    00055521
              DISPLAY '*                                          *'    00055621
              DISPLAY '*     ARQUIVO EVSA0407 ESTA VAZIO          *'    00055721
              DISPLAY '*                                          *'    00055821
              DISPLAY '*       PROCESSAMENTO  ENCERRADO           *'    00055921
              DISPLAY '*                                          *'    00056021
              DISPLAY '***************** EVSA0407 *****************'    00056121
           END-IF.                                                      00056221
                                                                        00056321
      *----------------------------------------------------------------*00056421
       2000-99-FIM.                            EXIT.                    00056521
      *----------------------------------------------------------------*00056621
                                                                        00056721
      ******************************************************************00056821
      *           LEITURA DO ARQUIVO DE ENTRADA - MOV1207              *00056926
      ******************************************************************00057021
                                                                        00057121
      *----------------------------------------------------------------*00057221
       2100-LER-MOV1207                         SECTION.                00057321
      *----------------------------------------------------------------*00057421
                                                                        00057521
           READ MOV1207                                                 00057621
                                                                        00057721
           IF WRK-FS-MOV1207                EQUAL '10'                  00057823
              GO                            TO 2100-99-FIM              00057921
           END-IF.                                                      00058021
                                                                        00058121
           MOVE WRK-LEITURA        TO WRK-FS-OPERACAO.                  00058221
           MOVE '2100'             TO WRK-FS-LOCAL.                     00058321
                                                                        00058421
           PERFORM 1100-FILE-STATUS.                                    00058521
                                                                        00058721
           PERFORM 2105-CONSISTIR-CAMPOS.                               00058821
                                                                        00058921
           IF WRK-INCONS   = 'S'                                        00059021
              ADD 1     TO ACUM-INCONS                                  00059121
              GO        TO 2100-LER-MOV1207                             00059221
           END-IF.                                                      00059321
                                                                        00059421
      *----------------------------------------------------------------*00059521
       2100-99-FIM.                              EXIT.                  00059621
      *----------------------------------------------------------------*00059721
                                                                        00059821
      ******************************************************************00059921
      *           CONSISTIR CAMPOS                                     *00060021
      ******************************************************************00060121
                                                                        00060221
      *----------------------------------------------------------------*00060321
       2105-CONSISTIR-CAMPOS                     SECTION.               00060421
      *----------------------------------------------------------------*00060521
                                                                        00060621
           MOVE 'N'           TO WRK-INCONS.                            00060721
                                                                        00060821
           IF (FD-AGENCIA     NOT NUMERIC) OR                           00060921
              (FD-AGENCIA     EQUAL ZEROS)                              00061021
               MOVE 'S'       TO WRK-INCONS                             00061121
               GO TO 2105-99-FIM                                        00061221
           END-IF.                                                      00061321
                                                                        00061421
           IF (FD-CONTA       NOT NUMERIC) OR                           00061521
              (FD-CONTA       EQUAL ZEROS)                              00061621
               MOVE 'S'       TO WRK-INCONS                             00061721
               GO TO 2105-99-FIM                                        00061821
           END-IF.                                                      00061921
                                                                        00062021
           IF (FD-SALARIO     NOT NUMERIC) OR                           00062121
              (FD-SALARIO     EQUAL ZEROS)                              00062221
               MOVE 'S'       TO WRK-INCONS                             00062321
               GO TO 2105-99-FIM                                        00062421
           END-IF.                                                      00062521
                                                                        00062621
      *----------------------------------------------------------------*00062721
       2105-99-FIM.                              EXIT.                  00062821
      *----------------------------------------------------------------*00062921
                                                                        00063021
      ******************************************************************00063121
      *                   PROCESSAMENTO PRINCIPAL                      *00063221
      ******************************************************************00063321
                                                                        00063421
      *----------------------------------------------------------------*00063521
       3000-PROCESSAR                       SECTION.                    00063621
      *----------------------------------------------------------------*00063721
                                                                        00063822
           INITIALIZE WRK-EVSA0407                                      00063922
                                                                        00064023
           MOVE FD-MOV1207            TO WRK-EVSA0407                   00064123
           MOVE FD-MOV1207            TO FD-CHAVE                       00064223
           READ EVSA0407                                                00064321
                                                                        00064424
            IF WRK-FS-EVSA0407        EQUAL ZEROS                       00064523
                MOVE FD-MOV1207       TO WRK-EVSA0407                   00064623
                REWRITE FD-EVSA0407   FROM WRK-EVSA0407                 00064723
                ADD 1                 TO ACUM-ATT-MOV1207               00064823
                                                                        00064919
                IF WRK-FS-EVSA0407    EQUAL ZEROS                       00065024
                   DISPLAY '--------------------------------------'     00065124
                   DISPLAY 'ATUALIZADO: ' WRK-EVSA0407                  00065224
                END-IF                                                  00065302
             ELSE                                                       00065421
               WRITE FD-EXC1207       FROM WRK-EVSA0407                 00065524
             END-IF.                                                    00065714
                                                                        00065821
           READ MOV1207.                                                00065902
           ADD 1                      TO ACUM-LIDOS-MOV1207.            00066024
                                                                        00066100
      *----------------------------------------------------------------*00066200
       3000-99-FIM.                             EXIT.                   00066300
      *----------------------------------------------------------------*00067000
                                                                        00073000
      ******************************************************************00074000
      *                       FINALIZACAO                              *00075000
      ******************************************************************00076000
                                                                        00077000
      *----------------------------------------------------------------*00078000
       4000-FINALIZAR                        SECTION.                   00079000
      *----------------------------------------------------------------*00080000
                                                                        00081000
           CLOSE EVSA0407                                               00081100
                 MOV1207                                                00081221
                 EXC1207.                                               00081321
                                                                        00081421
           MOVE WRK-FECHAMENTO     TO WRK-FS-OPERACAO.                  00081521
           MOVE '3000'             TO WRK-FS-LOCAL.                     00081621
                                                                        00081721
           PERFORM 1100-FILE-STATUS.                                    00081821
           PERFORM 4100-EMITIR-TOTAIS.                                  00081921
                                                                        00082021
      *----------------------------------------------------------------*00083200
       4000-99-FIM.                           EXIT.                     00083300
      *----------------------------------------------------------------*00083400
                                                                        00083500
      ******************************************************************00083600
      *                       EMITIR TOTAIS                            *00083700
      ******************************************************************00083800
                                                                        00083900
      *----------------------------------------------------------------*00084000
       4100-EMITIR-TOTAIS                    SECTION.                   00084100
      *----------------------------------------------------------------*00084200
                                                                        00084300
           DISPLAY '*-*-*-*-*-*-*-*-*-* MD021004 *-*-*-*-*-*-*-*-*-*-*' 00084428
           DISPLAY '*                                                *' 00084528
           DISPLAY '*             MD0201004 - PROCESSAMENTO OK !     *' 00084828
           DISPLAY '*                                                *' 00084928
           DISPLAY '* REGISTROS LIDOS => ' ACUM-LIDOS-MOV1207           00085434
           DISPLAY '*                                                *' 00085528
           DISPLAY '* REGISTROS ATUALIZADOS => ' ACUM-ATT-MOV1207       00085634
           DISPLAY '*                                                *' 00085728
           DISPLAY '* REGISTROS INCONSISTENTES => ' ACUM-INCONS         00085834
           DISPLAY '*                                                *' 00086128
           DISPLAY '*-*-*-*-*-*-*-*-*-* MD021004 *-*-*-*-*-*-*-*-*-*-*'.00086228
                                                                        00087200
      *----------------------------------------------------------------*00087300
       4100-99-FIM.                           EXIT.                     00087400
      *----------------------------------------------------------------*00087500
                                                                        00087600
      ******************************************************************00087700
      *                       TRATAMENTO DE ERRO                       *00087800
      ******************************************************************00087900
                                                                        00088000
      *----------------------------------------------------------------*00088100
       9999-TRATAR-ERRO                      SECTION.                   00088200
      *----------------------------------------------------------------*00088300
                                                                        00088400
           DISPLAY ' *** FIM DE PROCESSAMENTO ***'.                     00088505
           GOBACK.                                                      00088612
                                                                        00088700
      *----------------------------------------------------------------*00088800
       9999-99-FIM.                           EXIT.                     00088900
      *----------------------------------------------------------------*00089000
