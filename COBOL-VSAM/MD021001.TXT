      *================================================================*00001000
       IDENTIFICATION                            DIVISION.              00002000
      *================================================================*00002100
                                                                        00002200
       PROGRAM-ID.  MD021001.                                           00002300
       AUTHOR.      ADNAN.                                              00002400
                                                                        00002500
      *================================================================*00002600
      *                         F O U R S Y S                          *00002700
      *================================================================*00002800
      *    PROGRAMA...: MD021001                                       *00002900
      *    PROGAMADOR.: ADNAN CHRISTIAN                                *00003000
      *    EMPRESA....: FOURSYS                                        *00003100
      *    ANALISTA...: IVAN SANCHES                                   *00003200
      *    DATA.......: 06/07/2022                                     *00003300
      *----------------------------------------------------------------*00003400
      *    OBJETIVO : ESTE PROGRAMA TEM COMO OBJETIVO FAZER A LEITURA  *00003500
      *               DO ARQUIVOS EVSA0407, FORMATAR SEUS DADOS EM UM  *00003600
      *               ARQUIVO SEQUENCIAL COM REGISTROS ONDE O SALDO    *00003700
      *               MAIOR QUE 0; SALDOS MENORES SERAO DEPREZADOS NA  *00003800
      *               SYSOUT                                           *00003900
      *----------------------------------------------------------------*00004000
      *    ARQUIVOS....:                                               *00004100
      *      DDNAME                I/O             INCLUDE/BOOK        *00004200
      *     EVSA0407                I                B#EVSA            *00004303
      *     SVSA0407                O                B#SVSA            *00004400
      *----------------------------------------------------------------*00004600
      *    MODULOS....:                                                *00004700
      *    -------                                                     *00004907
      *----------------------------------------------------------------*00005007
      *    COPYBOOKS..:                                                *00005107
      *     B#GLOB - GRAVACAO DE ERROS                                 *00005207
      ******************************************************************00005307
      *================================================================*00005407
                                                                        00005507
      *================================================================*00005607
       ENVIRONMENT DIVISION.                                            00005707
      *================================================================*00005807
                                                                        00005907
      *----------------------------------------------------------------*00006007
       CONFIGURATION                              SECTION.              00006107
      *----------------------------------------------------------------*00006207
                                                                        00006307
       SPECIAL-NAMES.                                                   00006407
           DECIMAL-POINT IS COMMA.                                      00006507
                                                                        00006607
      *----------------------------------------------------------------*00006707
                                                                        00006807
      *----------------------------------------------------------------*00006907
       INPUT-OUTPUT                               SECTION.              00007007
      *----------------------------------------------------------------*00007107
                                                                        00007207
       FILE-CONTROL.                                                    00007307
             SELECT EVSA0407 ASSIGN     TO EVSA0407                     00007407
                ORGANIZATION            IS INDEXED                      00007507
                ACCESS MODE             IS SEQUENTIAL                   00007607
                RECORD KEY              IS FD-CHAVE                     00007707
                FILE STATUS             IS WRK-FS-EVSA0407.             00007807
                                                                        00007907
             SELECT SVSA0407 ASSIGN     TO SVSA0407                     00008007
                FILE STATUS             IS WRK-FS-SVSA0407.             00008107
                                                                        00008207
      *================================================================*00008307
       DATA                                      DIVISION.              00008407
      *================================================================*00008507
                                                                        00009000
      *----------------------------------------------------------------*00009100
       FILE                                      SECTION.               00009200
      *----------------------------------------------------------------*00009300
                                                                        00009400
      *----------------------------------------------------------------*00009500
      *                                                                *00009600
      *   INPUT - DADOS DO ARQUIVO DE ENTRADA (EVSA0407)               *00009700
      *                                                  - LRECL = 019 *00009800
      *----------------------------------------------------------------*00009900
                                                                        00010000
       FD EVSA0407.                                                     00010100
                                                                        00011000
       01 FD-EVSA0407.                                                  00012003
          05 FD-CHAVE.                                                  00013006
             10 FD-AGENCIA        PIC X(004).                           00014006
             10 FD-CONTA          PIC X(005).                           00014106
          05 FD-SALARIO           PIC 9(010).                           00014206
                                                                        00014300
      *----------------------------------------------------------------*00015000
      *                                                                *00015100
      *   INPUT - DADOS DO ARQUIVO DE SAIDA (SVSA0407)                 *00015200
      *                                                  - LRECL = 027 *00015300
      *----------------------------------------------------------------*00015400
                                                                        00015500
       FD SVSA0407                                                      00015600
           RECORDING MODE IS F                                          00015700
           BLOCK CONTAINS 0 RECORDS.                                    00015900
                                                                        00016000
       01 FD-SVSA0407 PIC X(027).                                       00016100
                                                                        00016300
      *----------------------------------------------------------------*00016900
       WORKING-STORAGE                            SECTION.              00017000
      *----------------------------------------------------------------*00017100
                                                                        00018000
      *----------------------------------------------------------------*00019000
       01  FILLER                      PIC  X(050)         VALUE        00019100
           '*** INICIO DA WORKING MD021001 ***'.                        00019200
      *----------------------------------------------------------------*00019300
                                                                        00019500
      *----------------------------------------------------------------*00019600
       01  FILLER                       PIC X(050)          VALUE       00019700
           '*** TESTE DE FILE STATUS ***'.                              00019800
      *----------------------------------------------------------------*00019900
                                                                        00020000
        77 WRK-FS-EVSA0407       PIC 9(002)         VALUE ZEROS.        00020100
        77 WRK-FS-SVSA0407       PIC 9(002)         VALUE ZEROS.        00020200
        77 WRK-FS-OPERACAO       PIC X(013)         VALUE SPACES.       00020300
        77 WRK-FS-LOCAL          PIC X(004)         VALUE SPACES.       00020400
                                                                        00020500
      *----------------------------------------------------------------*00020600
       01  FILLER                       PIC X(050)           VALUE      00020700
           '*** AREA DE AUXILIARES ***'.                                00020800
      *----------------------------------------------------------------*00020900
                                                                        00021000
        77 WRK-DATA              PIC 9(008).                            00021100
        77 WRK-INCONS            PIC X(001).                            00021200
                                                                        00021300
      *----------------------------------------------------------------*00021600
       01  FILLER                       PIC X(050)           VALUE      00022000
           '*** AREA DE ACUMULADORES ***'.                              00023000
      *----------------------------------------------------------------*00023100
                                                                        00023200
        77 ACUM-LIDOS-EVSA0407      PIC 9(009)      VALUE ZEROS.        00023300
        77 ACUM-GRAVS-SVSA0407      PIC 9(009)      VALUE ZEROS.        00023400
        77 ACUM-INCONS              PIC 9(009)      VALUE ZEROS.        00023500
                                                                        00023700
      *----------------------------------------------------------------*00023800
       01  FILLER                       PIC X(050)           VALUE      00023900
           '*** AREA DO B#GLOB ***'.                                    00024000
      *----------------------------------------------------------------*00025000
                                                                        00026000
        COPY 'B#GLOB'.                                                  00027000
                                                                        00027100
      *----------------------------------------------------------------*00027200
       01  FILLER                       PIC X(050)           VALUE      00027300
           '*** AREA DO B#EVSA ***'.                                    00027403
      *----------------------------------------------------------------*00027500
                                                                        00027600
        COPY 'B#EVSA'.                                                  00027704
                                                                        00027800
      *----------------------------------------------------------------*00027903
       01  FILLER                       PIC X(050)           VALUE      00028003
           '*** AREA DO B#SVSA ***'.                                    00029003
      *----------------------------------------------------------------*00029103
                                                                        00029203
        COPY 'B#SVSA'.                                                  00029303
                                                                        00029403
      *----------------------------------------------------------------*00029500
       01  FILLER                      PIC  X(050)         VALUE        00030000
           '*** MD021001 - FIM DA AREA DE WORKING ***'.                 00040000
      *----------------------------------------------------------------*00041000
                                                                        00041100
      *================================================================*00041200
        PROCEDURE                       DIVISION.                       00041300
      *================================================================*00041400
                                                                        00041500
      ******************************************************************00041600
      *                    ROTINA PRINCIPAL                           * 00041700
      ******************************************************************00041800
                                                                        00041900
      *----------------------------------------------------------------*00042000
       0000-PRINCIPAL                            SECTION.               00042100
      *----------------------------------------------------------------*00042200
                                                                        00042300
           PERFORM 1000-INICIAR.                                        00042400
                                                                        00042500
           PERFORM 2000-VERIFICAR-VAZIO.                                00042800
                                                                        00043000
           PERFORM 3000-PROCESSAR UNTIL WRK-FS-EVSA0407 EQUAL '10'.     00043100
                                                                        00043200
           PERFORM 4000-FINALIZAR.                                      00043300
                                                                        00043400
           STOP RUN.                                                    00043500
                                                                        00043600
      *----------------------------------------------------------------*00043700
       0000-99-FIM.                           EXIT.                     00043800
      *----------------------------------------------------------------*00043900
                                                                        00044000
      ******************************************************************00044100
      *                    PROCEDIMENTOS INICIAIS                     * 00044200
      ******************************************************************00044300
                                                                        00044400
      *----------------------------------------------------------------*00044500
       1000-INICIAR                           SECTION.                  00044600
      *----------------------------------------------------------------*00044700
                                                                        00044800
            OPEN INPUT   EVSA0407                                       00044900
                 OUTPUT  SVSA0407.                                      00045000
                                                                        00045100
            MOVE WRK-ABERTURA             TO WRK-FS-OPERACAO.           00045200
            MOVE '1000'                   TO WRK-FS-LOCAL.              00045300
            PERFORM 1100-TESTAR-FILE-STATUS.                            00045400
                                                                        00045500
            ACCEPT WRK-DATA FROM DATE YYYYMMDD.                         00045600
                                                                        00045700
      *----------------------------------------------------------------*00045800
       1000-99-FIM.                              EXIT.                  00045900
      *----------------------------------------------------------------*00046000
                                                                        00046100
      ******************************************************************00046200
      *               TESTE DE FILE-STATUS                             *00046300
      ******************************************************************00046400
                                                                        00046500
      *----------------------------------------------------------------*00046600
       1100-TESTAR-FILE-STATUS                   SECTION.               00046700
      *----------------------------------------------------------------*00046800
                                                                        00046900
            PERFORM 1110-TESTAR-FS-EVSA0407.                            00047000
                                                                        00047100
            PERFORM 1120-TESTAR-FS-SVSA0407.                            00047200
                                                                        00047300
      *----------------------------------------------------------------*00047500
       1100-99-FIM.                               EXIT.                 00047600
      *----------------------------------------------------------------*00047700
                                                                        00047800
      ******************************************************************00047900
      *    TESTAR FILE-STATUS DO ARQUIVO DE ENTRADA - EVSA0407         *00048000
      ******************************************************************00048100
                                                                        00048200
      *----------------------------------------------------------------*00048300
       1110-TESTAR-FS-EVSA0407                SECTION.                  00048400
      *----------------------------------------------------------------*00048500
                                                                        00048600
           IF WRK-FS-EVSA0407 NOT EQUAL ZEROS                           00048700
              MOVE 'MD021001'                 TO WRK-PROGRAMA           00048800
              MOVE WRK-FS-EVSA0407            TO WRK-STATUS             00048900
              MOVE WRK-FS-OPERACAO            TO WRK-MENSAGEM           00049000
                                              OF WRK-LOG                00049100
              MOVE WRK-FS-LOCAL               TO WRK-SECAO              00049200
              PERFORM 9999-TRATAR-ERRO                                  00049300
           END-IF.                                                      00049400
                                                                        00049500
      *----------------------------------------------------------------*00049600
       1110-99-FIM.                            EXIT.                    00049700
      *----------------------------------------------------------------*00049800
                                                                        00049900
      ******************************************************************00050000
      *    TESTAR FILE-STATUS DO ARQUIVO DE SAIDA - SVSA0407           *00050100
      ******************************************************************00050200
                                                                        00050300
      *----------------------------------------------------------------*00050400
       1120-TESTAR-FS-SVSA0407               SECTION.                   00050500
      *----------------------------------------------------------------*00050600
                                                                        00050700
           IF WRK-FS-SVSA0407 NOT EQUAL ZEROS                           00050800
              MOVE 'MD021001'                 TO WRK-PROGRAMA           00050900
              MOVE WRK-FS-EVSA0407            TO WRK-STATUS             00051000
              MOVE WRK-FS-OPERACAO            TO WRK-MENSAGEM           00051100
                                              OF WRK-LOG                00051200
              MOVE WRK-FS-LOCAL               TO WRK-SECAO              00051300
              PERFORM 9999-TRATAR-ERRO                                  00051400
                PERFORM 9999-TRATAR-ERRO                                00051700
           END-IF.                                                      00051900
                                                                        00052000
      *----------------------------------------------------------------*00052100
       1120-99-FIM.                            EXIT.                    00052200
      *----------------------------------------------------------------*00052300
                                                                        00053000
      *----------------------------------------------------------------*00058000
                                                                        00060800
      ******************************************************************00063100
      *                   VERIFICAR VAZIO                              *00063200
      ******************************************************************00063300
                                                                        00063400
      *----------------------------------------------------------------*00063500
       2000-VERIFICAR-VAZIO                 SECTION.                    00063600
      *----------------------------------------------------------------*00063700
                                                                        00065500
           PERFORM 2100-LER-EVSA0407.                                   00067100
           IF WRK-FS-EVSA0407              EQUAL '10'                   00067300
              DISPLAY '***************** EVSA0407 *****************'    00067400
              DISPLAY '*                                          *'    00068500
              DISPLAY '*     ARQUIVO EVSA0407 ESTA VAZIO          *'    00068600
              DISPLAY '*                                          *'    00068700
              DISPLAY '*        PROCESSAMENTO ENCERRADO           *'    00068800
              DISPLAY '*                                          *'    00068900
              DISPLAY '***************** EVSA0407 *****************'    00069000
           END-IF.                                                      00069100
                                                                        00069200
      *----------------------------------------------------------------*00069300
       2000-99-FIM.                            EXIT.                    00069400
      *----------------------------------------------------------------*00069500
                                                                        00069600
      ******************************************************************00069700
      *       LEITURA DO ARQUIVO DE ENTRADA - EVSA0407                 *00069800
      ******************************************************************00069900
                                                                        00070000
      *----------------------------------------------------------------*00070100
       2100-LER-EVSA0407                    SECTION.                    00070200
      *----------------------------------------------------------------*00070300
                                                                        00070400
           READ EVSA0407 INTO REG-EVSA0407.                             00070500
           IF WRK-FS-EVSA0407              EQUAL '10'                   00070600
              GO                           TO 2100-99-FIM               00070700
           END-IF.                                                      00070800
           MOVE WRK-LEITURA                TO WRK-FS-OPERACAO           00070900
           MOVE '2100'                     TO WRK-FS-LOCAL.             00071000
                                                                        00071100
           PERFORM 1100-TESTAR-FILE-STATUS.                             00071201
                                                                        00071300
           ADD 1 TO ACUM-LIDOS-EVSA0407.                                00071400
                                                                        00071500
           PERFORM 2105-CONSISTIR-CAMPOS                                00071600
                                                                        00071700
           IF WRK-INCONS  = 'S'                                         00071800
              ADD 1 TO ACUM-INCONS                                      00071900
              GO    TO 2100-LER-EVSA0407                                00072000
           END-IF.                                                      00072100
                                                                        00072200
      *----------------------------------------------------------------*00072400
       2100-99-FIM.                            EXIT.                    00072500
      *----------------------------------------------------------------*00072600
                                                                        00072700
      ******************************************************************00072800
      *         SECAO DE CONSISTENCIA DE CAMPOS                        *00072900
      ******************************************************************00073000
                                                                        00073100
      *----------------------------------------------------------------*00073200
       2105-CONSISTIR-CAMPOS                SECTION.                    00073301
      *----------------------------------------------------------------*00073400
                                                                        00073500
           MOVE 'N'          TO WRK-INCONS.                             00073600
                                                                        00075300
           IF (ARQ-AGENCIA     NOT NUMERIC) OR                          00075400
              (ARQ-AGENCIA     EQUAL ZEROS)                             00075500
               MOVE 'S'        TO WRK-INCONS                            00075600
               GO TO 2105-99-FIM                                        00075700
           END-IF.                                                      00075800
                                                                        00075900
           IF (ARQ-CONTA       NOT NUMERIC) OR                          00076000
              (ARQ-CONTA       EQUAL ZEROS)                             00076100
               MOVE 'S'        TO WRK-INCONS                            00076200
               GO TO 2105-99-FIM                                        00076300
           END-IF.                                                      00076400
                                                                        00076500
           IF (ARQ-SALARIO     NOT NUMERIC) OR                          00076600
              (ARQ-SALARIO     EQUAL ZEROS)                             00076700
               MOVE 'S'        TO WRK-INCONS                            00076800
               GO TO 2105-99-FIM                                        00076900
           END-IF.                                                      00077000
                                                                        00077100
      *----------------------------------------------------------------*00077200
       2105-99-FIM.                            EXIT.                    00077301
      *----------------------------------------------------------------*00077400
                                                                        00077500
      ******************************************************************00077600
      *                   PROCESSAMENTO PRINCIPAL                      *00077700
      ******************************************************************00077800
                                                                        00077900
      *----------------------------------------------------------------*00078000
       3000-PROCESSAR                       SECTION.                    00078100
      *----------------------------------------------------------------*00078200
                                                                        00078300
           INITIALIZE REG-SVSA0407.                                     00078400
                                                                        00078500
           MOVE ARQ-AGENCIA                TO REG-SVSA0407-AGENCIA.     00078605
           MOVE ARQ-CONTA                  TO REG-SVSA0407-CONTA.       00078705
           MOVE ARQ-SALARIO                TO REG-SVSA0407-SALARIO.     00078805
           MOVE WRK-DATA                   TO REG-SVSA0407-DATA.        00078905
                                                                        00079000
           PERFORM 3100-GRAVAR-SVSA0407.                                00079100
                                                                        00079200
           PERFORM 2100-LER-EVSA0407.                                   00079300
                                                                        00079400
      *----------------------------------------------------------------*00079500
       3000-99-FIM.                            EXIT.                    00079600
      *----------------------------------------------------------------*00079700
                                                                        00079800
      ******************************************************************00079900
      *          GRAVACAO DO ARQUIVO DE SAIDA SVSA0407                 *00080000
      ******************************************************************00080100
                                                                        00080200
      *----------------------------------------------------------------*00080300
       3100-GRAVAR-SVSA0407                 SECTION.                    00080400
      *----------------------------------------------------------------*00080500
                                                                        00080600
           WRITE FD-SVSA0407               FROM REG-CORPO.              00080703
                                                                        00080800
           MOVE WRK-GRAVACAO               TO WRK-FS-OPERACAO.          00080900
           MOVE '3100'                     TO WRK-FS-LOCAL.             00081000
                                                                        00081300
           PERFORM 1100-TESTAR-FILE-STATUS.                             00081406
                                                                        00081500
           ADD 1 TO ACUM-GRAVS-SVSA0407.                                00081600
                                                                        00081700
      *----------------------------------------------------------------*00081800
       3000-99-FIM.                            EXIT.                    00081900
      *----------------------------------------------------------------*00082000
                                                                        00082100
      ******************************************************************00082200
      *                       FINALIZACAO                              *00082300
      ******************************************************************00082400
                                                                        00082500
      *----------------------------------------------------------------*00082600
       4000-FINALIZAR                        SECTION.                   00082700
      *----------------------------------------------------------------*00082800
                                                                        00082900
           CLOSE EVSA0407                                               00083000
                 SVSA0407.                                              00083100
                                                                        00083200
           MOVE WRK-FECHAMENTO             TO WRK-FS-OPERACAO.          00083300
           MOVE '4000'                     TO WRK-FS-LOCAL.             00083400
                                                                        00083500
           PERFORM 1100-TESTAR-FILE-STATUS.                             00083601
                                                                        00083700
           PERFORM 4100-EMITIR-TOTAIS.                                  00083800
                                                                        00083900
      *----------------------------------------------------------------*00084000
       4000-99-FIM.                           EXIT.                     00084100
      *----------------------------------------------------------------*00084200
                                                                        00084300
      ******************************************************************00084400
      *                       EMITIR TOTAIS                            *00084500
      ******************************************************************00084600
                                                                        00084700
      *----------------------------------------------------------------*00084800
       4100-EMITIR-TOTAIS                    SECTION.                   00084900
      *----------------------------------------------------------------*00085000
                                                                        00085100
           DISPLAY '*-*-*-*-*-*-*-*-*- MD021001 *-*-*-*-*-*-*-*-*-*-*-'.00085200
           DISPLAY '*                                                *'.00085300
           DISPLAY '*                                                *'.00085400
           DISPLAY '*                REGISTROS LIDOS                 *'.00085500
           DISPLAY '*                                                *'.00085600
           DISPLAY '*                                                *'.00085700
           DISPLAY '*  TOTAIS LIDOS EVSA0407: ' ACUM-LIDOS-EVSA0407.    00085800
           DISPLAY '*                                                *'.00085900
           DISPLAY '*                                                *'.00086000
           DISPLAY '*                REGISTROS GRAVADOS              *'.00086100
           DISPLAY '*                                                *'.00086200
           DISPLAY '*                                                *'.00086300
           DISPLAY '*  TOTAIS GRAVADOS SVSA0407: ' ACUM-GRAVS-SVSA0407. 00086400
           DISPLAY '*                                                *'.00086500
           DISPLAY '*                                                *'.00086600
           DISPLAY '*                REGISTROS INCONSISTENTES        *'.00086700
           DISPLAY '*                                                *'.00086800
           DISPLAY '*                                                *'.00086900
           DISPLAY '*  TOTAIS INCONSISTIDOS: ' ACUM-INCONS.             00087000
           DISPLAY '*                                                *'.00087100
           DISPLAY '*                                                *'.00087200
           DISPLAY '*-*-*-*-*-*-*-*-*- MD021001 *-*-*-*-*-*-*-*-*-*-*-'.00087300
                                                                        00087400
      *----------------------------------------------------------------*00087500
       4100-99-FIM.                           EXIT.                     00087600
      *----------------------------------------------------------------*00087700
                                                                        00087800
      ******************************************************************00087900
      *                       TRATAMENTO DE ERRO                       *00088000
      ******************************************************************00088100
                                                                        00088200
      *----------------------------------------------------------------*00088300
       9999-TRATAR-ERRO                      SECTION.                   00088400
      *----------------------------------------------------------------*00088500
                                                                        00088600
           DISPLAY ' *** FIM DE PROCESSAMENTO *** '                     00088700
           GOBACK.                                                      00088800
                                                                        00088900
      *----------------------------------------------------------------*00089000
       9999-99-FIM.                           EXIT.                     00089100
      *----------------------------------------------------------------*00090000
