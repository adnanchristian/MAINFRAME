      *================================================================*00001000
       IDENTIFICATION                            DIVISION.              00002000
      *================================================================*00002100
                                                                        00002200
       PROGRAM-ID.  FR10DB03.                                           00002300
       AUTHOR.      ADNAN.                                              00002400
                                                                        00002500
      *================================================================*00002600
      *                         F O U R S Y S                          *00002700
      *================================================================*00002800
      *    PROGRAMA...: FR10DB03                                       *00002900
      *    TIPO.......: SERVICO DE ACESSO A DADOS                      *00003000
      *----------------------------------------------------------------*00003100
      *    PROGAMADOR.: ADNAN CHRISTIAN                                *00003200
      *    EMPRESA....: FOURSYS                                        *00003300
      *    ANALISTA...: IVAN SANCHES                                   *00003400
      *    DATA.......: 03/06/2022                                     *00003500
      *----------------------------------------------------------------*00003600
      *    OBJETIVO : ESTE PROGRAMA TEM A FINALIDADE DE INSERIR DADOS  *00003710
      *               NA TABELA FOUR001.FUNC                           *00003810
      *----------------------------------------------------------------*00003900
      *    BASE DE DADOS:                                              *00004000
      *      TABELAS DB2                           INCLUDE/BOOK        *00004100
      *      FOUR001.FUNC                           BOOKFUNC           *00004200
      *----------------------------------------------------------------*00004300
      *    MODULOS.......:                                             *00004400
      *     GRAVALOG  - TRATAMENTO DE ERROS                            *00004500
      *----------------------------------------------------------------*00004600
      *    COPYBOOKS.....:                                             *00004700
      *     B#GRAVLOG - AREA DE TRATAMENTO DE ERROS                    *00004800
      *================================================================*00005000
                                                                        00005100
      *================================================================*00005200
       ENVIRONMENT DIVISION.                                            00005300
      *================================================================*00005400
                                                                        00005500
      *----------------------------------------------------------------*00005600
       CONFIGURATION                              SECTION.              00005700
      *----------------------------------------------------------------*00005800
                                                                        00005900
       SPECIAL-NAMES.                                                   00006000
           DECIMAL-POINT IS COMMA.                                      00006100
                                                                        00006200
      *----------------------------------------------------------------*00006300
                                                                        00006400
      *================================================================*00006500
       DATA                DIVISION.                                    00006600
      *================================================================*00006700
      *----------------------------------------------------------------*00006800
       WORKING-STORAGE                            SECTION.              00006900
      *----------------------------------------------------------------*00007000
                                                                        00008000
      *----------------------------------------------------------------*00009000
       01  FILLER                      PIC  X(050)         VALUE        00010000
           '*** INICIO DA WORKING FR10DB03 ***'.                        00011000
      *----------------------------------------------------------------*00012000
                                                                        00012107
      *----------------------------------------------------------------*00012207
       01  FILLER                       PIC X(050)           VALUE      00012307
           '*** AREA DB2 ***'.                                          00012407
      *----------------------------------------------------------------*00012507
                                                                        00012607
           EXEC SQL                                                     00012707
             INCLUDE BOOKFUNC                                           00012807
           END-EXEC.                                                    00012907
           EXEC SQL                                                     00013007
               INCLUDE SQLCA                                            00013107
           END-EXEC.                                                    00013207
                                                                        00013400
      *----------------------------------------------------------------*00014000
       01  FILLER                       PIC X(050)           VALUE      00015000
           '*** AREA DE AUXILIARES ***'.                                00016000
      *----------------------------------------------------------------*00017000
                                                                        00018000
       77 WRK-SQLCODE            PIC -999.                              00020000
       77 WRK-INDICATOR          PIC S9(004) COMP VALUE ZEROS.          00021000
                                                                        00021100
      *----------------------------------------------------------------*00021202
       01  FILLER                       PIC X(050)           VALUE      00021302
           '*** AREA DE DADOS ***'.                                     00021402
      *----------------------------------------------------------------*00021502
                                                                        00021602
          01 WRK-ID.                                                    00021700
             02 FILLER           PIC X(010).                            00021807
             02 WRK-ID-AC        PIC 9(004).                            00021907
                                                                        00022000
          01 WRK-NOME.                                                  00022100
             02 FILLER           PIC X(010).                            00022207
             02 WRK-NOME-AC      PIC X(030).                            00022307
                                                                        00022400
          01 WRK-SETOR.                                                 00022500
             02 FILLER           PIC X(010).                            00022607
             02 WRK-SETOR-AC     PIC X(004).                            00022700
                                                                        00022800
          01 WRK-SALARIO.                                               00022900
             02 FILLER            PIC X(010).                           00023000
             02 WRK-SALARIO-AC    PIC 9(008)V99.                        00023100
                                                                        00023200
          01 WRK-DATAADM.                                               00023300
             02 FILLER            PIC X(010).                           00023400
             02 WRK-DATAADM-AC    PIC X(010).                           00023500
                                                                        00023600
          01 WRK-EMAIL.                                                 00023700
             02 FILLER            PIC X(010).                           00023800
             02 WRK-EMAIL-AC      PIC X(040).                           00023900
                                                                        00024000
      *----------------------------------------------------------------*00024100
       01  FILLER                       PIC X(050)           VALUE      00024200
           '*** AREA DO GRAVALOG ***'.                                  00024300
      *----------------------------------------------------------------*00024400
                                                                        00024500
        COPY 'B#GRVLOG'.                                                00024600
                                                                        00024700
      *----------------------------------------------------------------*00026000
       01  FILLER                      PIC  X(050)         VALUE        00026100
           '*** FR10DB03 - FIM DA AREA DE WORKING ***'.                 00026200
      *----------------------------------------------------------------*00027000
                                                                        00028000
      *================================================================*00029000
        PROCEDURE                       DIVISION.                       00030000
      *================================================================*00040000
                                                                        00041000
      ******************************************************************00041100
      *                    ROTINA PRINCIPAL                            *00041217
      ******************************************************************00041300
                                                                        00041400
      *----------------------------------------------------------------*00041500
       0000-PRINCIPAL                            SECTION.               00041600
      *----------------------------------------------------------------*00041700
                                                                        00041800
           PERFORM 1000-INICIAR.                                        00041907
                                                                        00042000
           PERFORM 2000-PROCESSAR.                                      00042107
                                                                        00042200
           PERFORM 3000-FINALIZAR.                                      00042300
                                                                        00042400
           STOP RUN.                                                    00042500
                                                                        00042600
      *----------------------------------------------------------------*00042700
       0000-99-FIM.                           EXIT.                     00042800
      *----------------------------------------------------------------*00042900
                                                                        00043000
      ******************************************************************00043100
      *                    PROCEDIMENTOS INICIAIS                     * 00043200
      ******************************************************************00043300
                                                                        00043400
      *----------------------------------------------------------------*00043500
       1000-INICIAR                           SECTION.                  00043600
      *----------------------------------------------------------------*00043700
                                                                        00043800
           ACCEPT WRK-ID.                                               00043908
           ACCEPT WRK-NOME.                                             00044008
           ACCEPT WRK-SETOR.                                            00044108
           ACCEPT WRK-SALARIO.                                          00045008
           ACCEPT WRK-DATAADM.                                          00045108
           ACCEPT WRK-EMAIL.                                            00045208
                                                                        00045300
      *----------------------------------------------------------------*00045406
       1000-99-FIM.                          EXIT.                      00045506
      *----------------------------------------------------------------*00045606
                                                                        00045817
      ******************************************************************00045917
      *                    PROCESSAMENTO PRINCIPAL                    * 00046017
      ******************************************************************00046117
                                                                        00046217
      *----------------------------------------------------------------*00046700
       2000-PROCESSAR                       SECTION.                    00046800
      *----------------------------------------------------------------*00046900
                                                                        00047918
           PERFORM 2100-MOVIMENTAR-VARIAVEIS.                           00048019
                                                                        00066100
      *----------------------------------------------------------------*00066214
       2000-99-FIM.                            EXIT.                    00066314
      *----------------------------------------------------------------*00066414
                                                                        00066518
      ******************************************************************00066618
      *                    MOVIMENTAR VARIAVEIS                        *00066718
      ******************************************************************00066818
                                                                        00066918
      *----------------------------------------------------------------*00067018
       2100-MOVIMENTAR-VARIAVEIS            SECTION.                    00067119
      *----------------------------------------------------------------*00067218
                                                                        00067318
           MOVE   WRK-ID-AC               TO DB2-ID.                    00067418
           MOVE   WRK-NOME-AC             TO DB2-NOME.                  00067518
           MOVE   WRK-SETOR-AC            TO DB2-SETOR.                 00067618
           MOVE   WRK-SALARIO-AC          TO DB2-SALARIO.               00067718
           MOVE   WRK-DATAADM-AC          TO DB2-DATAADM.               00067818
           MOVE   WRK-EMAIL-AC            TO DB2-EMAIL.                 00067918
                                                                        00068018
           PERFORM 2200-INSERIR-DADOS.                                  00068118
                                                                        00068218
      *----------------------------------------------------------------*00068518
       2100-99-FIM.                            EXIT.                    00068618
      *----------------------------------------------------------------*00068718
                                                                        00068818
      ******************************************************************00068918
      *               INSERIR DADOS NA TABELA FOUR001.FUNC             *00069018
      ******************************************************************00069118
                                                                        00069218
      *----------------------------------------------------------------*00069318
       2200-INSERIR-DADOS                   SECTION.                    00069418
      *----------------------------------------------------------------*00069518
                                                                        00069618
           EXEC SQL                                                     00069718
           INSERT INTO                                                  00069818
             FOUR001.FUNC(ID,NOME,SETOR,SALARIO,DATAADM,EMAIL)          00069918
           VALUES(:DB2-ID,                                              00070018
                  :DB2-NOME,                                            00070118
                  :DB2-SETOR,                                           00070218
                  :DB2-SALARIO,                                         00070318
                  :DB2-DATAADM,                                         00070418
                  :DB2-EMAIL)                                           00070518
           END-EXEC.                                                    00070618
                                                                        00070718
           PERFORM 2300-LISTAR-DADOS.                                   00070818
                                                                        00070918
      *----------------------------------------------------------------*00071018
       2200-99-FIM.                            EXIT.                    00071118
      *----------------------------------------------------------------*00071218
                                                                        00071318
      ******************************************************************00071418
      *                  LISTAR DADOS                                  *00071518
      ******************************************************************00071618
                                                                        00071718
      *----------------------------------------------------------------*00071818
       2300-LISTAR-DADOS                    SECTION.                    00071918
      *----------------------------------------------------------------*00072018
                                                                        00072118
           EVALUATE SQLCODE                                             00072218
           WHEN 0                                                       00072318
           DISPLAY 'DADOS INSERIDOS'                                    00072418
           DISPLAY 'ID.....: ' DB2-ID                                   00072518
           DISPLAY 'NOME...: ' DB2-NOME                                 00072618
           DISPLAY 'SETOR..: ' DB2-SETOR                                00072718
           DISPLAY 'SALARIO: ' DB2-SALARIO                              00072818
           DISPLAY 'DATAADM: ' DB2-DATAADM                              00072918
           DISPLAY 'EMAIL..: ' DB2-EMAIL                                00073018
                                                                        00073118
           WHEN -181                                                    00073218
               MOVE 'FR10DB03'                TO WRK-PROGRAMA           00073318
               MOVE 'DATA NO FORMATO ERRADO'  TO WRK-MENSAGEM           00073418
               MOVE '2000'                    TO WRK-SECAO              00073518
               MOVE SQLCODE                   TO WRK-STATUS             00073618
               PERFORM 9999-TRATAR-ERRO                                 00073720
                                                                        00073818
           WHEN OTHER                                                   00073918
                 MOVE 'FR10DB03'              TO WRK-PROGRAMA           00074018
                 MOVE 'ERRO....'              TO WRK-MENSAGEM           00074118
                 MOVE '2000'                  TO WRK-SECAO              00074218
                 MOVE SQLCODE                 TO WRK-STATUS             00074318
                 PERFORM 9999-TRATAR-ERRO                               00074420
            END-EVALUATE.                                               00074518
                                                                        00074618
      *----------------------------------------------------------------*00074718
       2300-99-FIM.                            EXIT.                    00074818
      *----------------------------------------------------------------*00075018
                                                                        00076400
      ******************************************************************00076500
      *                       FINALIZACAO                              *00076600
      ******************************************************************00076700
                                                                        00076800
      *----------------------------------------------------------------*00076900
       3000-FINALIZAR                        SECTION.                   00077000
      *----------------------------------------------------------------*00077100
                                                                        00077200
           DISPLAY ' *** FIM DE PROCESSAMENTO *** '.                    00077302
                                                                        00077400
      *----------------------------------------------------------------*00077500
       3000-99-FIM.                           EXIT.                     00077600
      *----------------------------------------------------------------*00077700
                                                                        00077800
      ******************************************************************00077900
      *                       TRATAMENTO DE ERRO                       *00078000
      ******************************************************************00079000
                                                                        00080000
      *----------------------------------------------------------------*00080100
       9999-TRATAR-ERRO                      SECTION.                   00080220
      *----------------------------------------------------------------*00080300
                                                                        00080400
           CALL 'GRAVALOG'      USING WRK-LOG.                          00080500
                                                                        00080600
           GOBACK.                                                      00080700
                                                                        00080800
      *----------------------------------------------------------------*00080900
       9999-99-FIM.                           EXIT.                     00081000
      *----------------------------------------------------------------*00082000
